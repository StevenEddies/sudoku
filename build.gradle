buildscript {
	repositories {
		jcenter()
	}
	dependencies {
		classpath 'info.solidsoft.gradle.pitest:gradle-pitest-plugin:1.1.9'
	}
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'
apply plugin: 'info.solidsoft.pitest'

version = project.findProperty("buildVersion") ?: "0.0-dev.0"
group = 'uk.me.eddies'

repositories {
	jcenter()
}

dependencies {
	compile 'org.slf4j:slf4j-api:1.7.21'
	runtime 'org.slf4j:slf4j-simple:1.7.21'
	testCompile 'junit:junit:4.12'
	testCompile 'org.mockito:mockito-core:1.10.19'
	testCompile 'org.hamcrest:hamcrest-all:1.3'
	testCompile 'pl.pragmatists:JUnitParams:1.0.5'
}


tasks.withType(Test) {
	ignoreFailures = true;
}

test {
	filter {
		includeTestsMatching "*Test"
	}
	reports.junitXml.destination = "$buildDir/reports/unit-test"
}

task integrationTest(type: Test) {
	filter {
		includeTestsMatching "*IT"
	}
	reports.junitXml.destination = "$buildDir/reports/integration-test"
}

task testJar(type: Jar) {
	classifier = 'test'
	from sourceSets.test.output
}

task setBuildProperties << {
	def props = new Properties()
	props.put('uk.me.eddies.apps.sudoku.version', version)
	def writer = new FileWriter(file('src/main/resources/uk/me/eddies/apps/sudoku/build.properties'))
	try {
		props.store(writer, 'Generated build properties')
		writer.flush()
	} finally {
		writer.close()
	}
}

pitest {
	threads = 1
	outputFormats = ['XML', 'HTML']
}

setBuildProperties {
	outputs.file 'src/main/resources/uk/me/eddies/apps/sudoku/build.properties'
}

processResources {
	dependsOn setBuildProperties
}

artifacts {
  archives testJar
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}
	repositories {
		maven {
			url "https://jenkins.eddies.me.uk/archiva/repository/internal/"
			credentials {
				username project.findProperty("archivaUsername")
				password project.findProperty("archivaPassword")
			}
		}
	}
}
